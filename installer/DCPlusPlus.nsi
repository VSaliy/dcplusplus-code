!include "MUI2.nsh"
!include "Sections.nsh"

!ifndef NSIS_UNICODE
	!error "An Unicode version of NSIS is required, see <http://code.google.com/p/unsis/>"
!endif

Var VERSION ; will be filled in onInit
!macro GetDCPlusPlusVersion
	; Get the DC++ version and store it in $VERSION
	GetDllVersionLocal "DCPlusPlus.exe" $R0 $R1
	IntOp $R2 $R0 / 0x00010000
	IntOp $R3 $R0 & 0x0000FFFF
	IntOp $R4 $R1 / 0x00010000
	IntOp $R5 $R1 & 0x0000FFFF
	StrCpy $VERSION "$R2.$R3$R4$R5"
!macroend

; The name of the installer
Name "DC++ $VERSION"

RequestExecutionLevel admin
SetCompressor /SOLID "lzma"
ShowInstDetails show
ShowUninstDetails show
XPStyle on

; The file to write
OutFile "DCPlusPlus-xxx.exe"

; The default installation directory
InstallDir $PROGRAMFILES\DC++
; Registry key to check for directory (so if you install again, it will 
; overwrite the old one automatically)
InstallDirRegKey HKLM SOFTWARE\DC++ "Install_Dir"

; Modern UI instructions

!define MUI_ICON "Install.ico"
!define MUI_UNICON "Uninstall.ico"

!define MUI_ABORTWARNING
!define MUI_ABORTWARNING_CANCEL_DEFAULT
!define MUI_UNABORTWARNING
!define MUI_UNABORTWARNING_CANCEL_DEFAULT

!define MUI_LANGDLL_ALWAYSSHOW
!define MUI_LANGDLL_REGISTRY_ROOT "HKLM"
!define MUI_LANGDLL_REGISTRY_KEY "SOFTWARE\DC++"
!define MUI_LANGDLL_REGISTRY_VALUENAME "Install_Language"
!define MUI_LANGDLL_WINDOWTITLE "$(^NameDA) language selection"

!define MUI_COMPONENTSPAGE_NODESC
!insertmacro MUI_PAGE_COMPONENTS

!insertmacro MUI_PAGE_DIRECTORY

!insertmacro MUI_PAGE_INSTFILES

!define MUI_UNCONFIRMPAGE_TEXT_TOP "$(^UninstallingText) $(WARNING_LOCALE_ERASED)"
!insertmacro MUI_UNPAGE_CONFIRM

!insertmacro MUI_UNPAGE_INSTFILES

; i18n.nsh should be generated by the build script to contain a list of all the languages.
!include "i18n.nsh"
!insertmacro MUI_RESERVEFILE_LANGDLL

; The stuff to install
Section $(SECTION_DCPP) dcpp
  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  ; Check for existing settings in the profile first
  IfFileExists "$APPDATA\DC++\*.xml" 0 check_programdir
  StrCpy $R0 "$APPDATA\DC++\BACKUP"
  MessageBox MB_YESNO|MB_ICONQUESTION $(BACKUP_QUESTION) IDNO no_backup
  CreateDirectory "$APPDATA\DC++\BACKUP\"
  CopyFiles "$APPDATA\DC++\*.xml" "$APPDATA\DC++\BACKUP\"
  goto no_backup

check_programdir:
  ; Maybe we're upgrading from an older version so lets check the program directory
  ; Delete a possibly existing dcppboot.xml to be able to check for existing local settings 
  Delete "$INSTDIR\dcppboot.xml"
  IfFileExists "$INSTDIR\*.xml" 0 no_backup
  StrCpy $R0 "$INSTDIR\BACKUP"
  MessageBox MB_YESNO|MB_ICONQUESTION $(BACKUP_QUESTION) IDNO no_backup
  CreateDirectory "$INSTDIR\BACKUP\"
  CopyFiles "$INSTDIR\*.xml" "$INSTDIR\BACKUP\"

no_backup:
  ; Put file there
  File "changelog.txt"
  File "cshelp.rtf"
  File "dcppboot.xml"
  File "DCPlusPlus.chm"
  File "DCPlusPlus.exe"
  File "DCPlusPlus.pdb"
  File "License.txt"
  File "ThirdPartyLicenses.txt"

  ; Add the whole locale directory
  File /r "locale"

  ; Remove opencow just in case we're upgrading
  Delete "$INSTDIR\opencow.dll"

  ; Write the installation path into the registry
  WriteRegStr HKLM SOFTWARE\DC++ "Install_Dir" "$INSTDIR"
  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "InstallLocation" "$INSTDIR"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "DisplayIcon" '"$INSTDIR\DCPlusPlus.exe"'
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "DisplayName" "DC++ $VERSION"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "DisplayVersion" "$VERSION"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "Publisher" "Jacek Sieka"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "URLInfoAbout" "http://dcplusplus.sourceforge.net/"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "URLUpdateInfo" "http://dcplusplus.sourceforge.net/download/"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "HelpLink" "http://dcplusplus.sourceforge.net/webhelp/"
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "NoModify" "1"
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++" "NoRepair" "1"

  WriteUninstaller "uninstall.exe"
SectionEnd

Section $(SECTION_START_MENU)
  CreateDirectory "$SMPROGRAMS\DC++"
  CreateShortCut "$SMPROGRAMS\DC++\DC++.lnk" "$INSTDIR\DCPlusPlus.exe" "" "$INSTDIR\DCPlusPlus.exe" 0 "" "" "$(SM_DCPP_DESCR)"
  CreateShortCut "$SMPROGRAMS\DC++\$(SM_LICENSE).lnk" "$INSTDIR\License.txt"
  CreateShortCut "$SMPROGRAMS\DC++\$(SM_HELP).lnk" "$INSTDIR\DCPlusPlus.chm"
  CreateShortCut "$SMPROGRAMS\DC++\$(SM_CHANGELOG).lnk" "$INSTDIR\changelog.txt"
  CreateShortCut "$SMPROGRAMS\DC++\$(SM_UNINSTALL).lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
SectionEnd

Section $(SECTION_LOCAL) loc
  ; Change to nonlocal dcppboot if the checkbox left checked
  SetOutPath $INSTDIR
  File /oname=dcppboot.xml "dcppboot.nonlocal.xml" 
SectionEnd

Function .onSelChange
  ; Do not show the warning on XP or older
  StrCmp $R8 "0" skip
  ; Show the warning only once
  StrCmp $R9 "1" skip
  SectionGetFlags ${loc} $0
  IntOp $0 $0 & ${SF_SELECTED}
  StrCmp $0 ${SF_SELECTED} skip
    StrCpy $R9 "1"
	MessageBox MB_OK|MB_ICONEXCLAMATION $(LOCAL_WARNING)
skip:
FunctionEnd

Function .onInit
	; Check for Vista+
	ReadRegStr $R8 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
	IfErrors xp_or_below nt
nt:
	StrCmp $R8 '6.0' vistaplus
	StrCmp $R8 '6.1' 0 xp_or_below
vistaplus:
	StrCpy $R8 "1"
	goto end
xp_or_below:
	StrCpy $R8 "0"
end:

	; Set the program component really required (read only)
	IntOp $0 ${SF_SELECTED} | ${SF_RO}
	SectionSetFlags ${dcpp} $0

	!insertmacro GetDCPlusPlusVersion

	!insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

; uninstall stuff

Function un.onInit
	!insertmacro GetDCPlusPlusVersion

	!insertmacro MUI_UNGETLANGUAGE
FunctionEnd

; special uninstall section.
Section "un.Uninstall"
  ; remove registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\DC++"
  DeleteRegKey HKLM SOFTWARE\DC++
  ; remove files
  Delete "$INSTDIR\DCPlusPlus.exe"
  Delete "$INSTDIR\changelog.txt"
  Delete "$INSTDIR\cshelp.rtf"
  Delete "$INSTDIR\dcppboot.xml"
  Delete "$INSTDIR\DCPlusPlus.chm"
  Delete "$INSTDIR\DCPlusPlus.pdb"
  Delete "$INSTDIR\License.txt"
  Delete "$INSTDIR\LICENSE-GeoIP.txt" ; now in ThirdPartyLicenses; remove if present.
  Delete "$INSTDIR\LICENSE-OpenSSL.txt" ; now in ThirdPartyLicenses; remove if present.
  Delete "$INSTDIR\mingwm10.dll" ; no longer required, remove if present.
  Delete "$INSTDIR\GeoIPCountryWhois.csv" ; no longer required, remove if present.
  Delete "$INSTDIR\ThirdPartyLicenses.txt"

  ; Delete the whole locale directory
  RMDir /r "$INSTDIR\locale"

  ; Remove registry entries
  ; Assume they are all registered to us
  DeleteRegKey HKCU "Software\Classes\dchub"
  DeleteRegKey HKCU "Software\Classes\adc"
  DeleteRegKey HKCU "Software\Classes\adcs"
  DeleteRegKey HKCU "Software\Classes\magnet"
  ; MUST REMOVE UNINSTALLER, too
  Delete $INSTDIR\uninstall.exe
  ; remove shortcuts, if any.
  Delete "$SMPROGRAMS\DC++\*.*"
  ; remove directories used.
  RMDir "$SMPROGRAMS\DC++"

  MessageBox MB_YESNO|MB_ICONQUESTION $(UN_REMOVE_QUESTION) IDYES kill_dir

  RMDir "$INSTDIR"
  goto end_uninstall

kill_dir:
  ; delete program directory
  RMDir /r "$INSTDIR"
  ; delete profile data directories
  RMDir /r "$APPDATA\DC++"
  RMDir /r "$LOCALAPPDATA\DC++"  

end_uninstall:

SectionEnd
