# vim: set filetype=py
 
Import('dev source_path')

env, target, sources = dev.prepare_build(source_path, 'xsum', source_glob="xsum.cpp", in_bin=False)

if 'msvc' in env['TOOLS']:
	if env['mode'] == 'debug':
		env.Prepend(LIBS = ['ssleay32d', 'libeay32d'])
	else:
		env.Prepend(LIBS = ['ssleay32', 'libeay32'])
else:
	env.Prepend(LIBS = ['ssl', 'crypto'])

if 'HAVE_HTMLHELP_H' in env['CPPDEFINES']:
	env.Append(LIBS='htmlhelp')

env.Append(LIBS = ['comctl32', 'ws2_32', 'ole32', 'gdi32', 'comdlg32', 'iphlpapi', 'winmm', 'shlwapi', 'oleaut32', 'uuid'])

env.Append(CPPPATH = ['#/openssl/include', '#/miniupnpc', '#/dwt/include', '#/', '#/bzip2'])

if '-mwindows' in env['CCFLAGS']:
	env['CCFLAGS'].remove('-mwindows')

if '-mwindows' in env['LINKFLAGS']:
	env['LINKFLAGS'].remove('-mwindows')

env.Append(CCFLAGS = ['-mconsole'])	
env.Append(LINKFLAGS = ['-mconsole'])	
	
openssl_lib = '#/openssl/lib/'
if env['arch'] != 'x86':
	openssl_lib += env['arch'] + '/'
env.Append(LIBPATH = [openssl_lib])

ret = env.Program(target, [sources, dev.client, dev.dwarf, dev.zlib, dev.boost, dev.bzip2, dev.geoip, dev.miniupnpc, dev.natpmp, dev.intl])

Return('ret')
